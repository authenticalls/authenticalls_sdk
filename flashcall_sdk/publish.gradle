apply plugin: 'maven-publish'

def LIB_GROUP_ID = 'com.authenticalls.libraries'
def LIB_ARTIFACT_ID = 'flashcall_sdk'
def LIB_VERSION = '1.0.0'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

Properties properties = new Properties()
properties.load(project.rootProject.file('secrets.properties').newDataInputStream())

publishing {
    repositories {
        maven {
            name = "FlashcallGithubPackagesPublish"
            url = uri("https://maven.pkg.github.com/authenticalls/authenticalls_sdk")
            credentials {
                username = properties.getProperty("GITHUB_USERNAME") ?: System.getenv("USERNAME")
                password = properties.getProperty("GITHUB_WRITE_TOKEN") ?: System.getenv("TOKEN")
            }
        }
        maven {
            name = 'LocalTestMavenRepo'
            url = "file://${buildDir}/repo"
        }
    }

    publications {
        flashcall_sdk(MavenPublication) {
            groupId LIB_GROUP_ID
            artifactId LIB_ARTIFACT_ID
            version LIB_VERSION
            artifact("$buildDir/outputs/aar/flashcall_sdk-release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}